#version 460
#extension GL_EXT_shader_atomic_float : enable
#extension GL_GOOGLE_include_directive : enable
#extension GL_KHR_shader_subgroup_arithmetic: enable

layout (binding = 0) buffer Data{
	float d[];
}data;

layout(binding = 1) buffer IndexBuffer{
	uint ind[];
};

layout(binding = 2, r8 ) uniform imageBuffer act;

layout(binding = 3) buffer pearson{
    uint maxIndex;
    uint amtOfAttributes;
    uint baseAttribute;
    uint amtOfActiveIndices;
    float res[];    //contains: float[amtOfAttributes] means,  
                    //          float[amtOfAttributes] nominator,
                    //          float[amtOfAttributes] denom1,
                    //          float[amtOfAttributes] denom2
};

#include "dataAccess.glsl"

layout (local_size_x = 256,local_size_y=1,local_size_z=1) in;

void main(){
    if(gl_GlobalInvocationID.x >= maxIndex) return;
    uint index = ind[gl_GlobalInvocationID.x];
    bool ac = bool(imageLoad(act, int(index)).x);
    if(!ac) return;
    for(int a = 0; a < amtOfAttributes; ++a){
        float aDiff = getPackedData(index, a) - res[a];
        float bDiff = getPackedData(index, baseAttribute) - res[a];
        float nominator = aDiff * bDiff;
        float denom1 = aDiff * aDiff;
        float denom2 = bDiff * bDiff;
        nominator = subgroupAdd(nominator);
        denom1 = subgroupAdd(denom1);
        denom2 = subgroupAdd(denom2);
        if(subgroupElect()){
            atomicAdd(res[amtOfAttributes + a], nominator);
            atomicAdd(res[2 * amtOfAttributes + a], denom1);
            atomicAdd(res[3 * amtOfAttributes + a], denom2);
        }
    }
}