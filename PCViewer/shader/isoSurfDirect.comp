#version 450
#extension GL_ARB_separate_shader_objects : enable
#extension GL_EXT_scalar_block_layout : enable
#extension GL_EXT_nonuniform_qualifier : enable

layout(std430, binding = 0) buffer Informations{
	uint amtOfAttributes;
	uint amtOfBrushAttributes;
	uint amtOfIndices;
	uint dimX;

	uint dimY;
	uint dimZ;
	uint xInd;
	uint yInd;

	uint zInd;
	float xMin;
	float xMax;
	float yMin;

	float yMax;
	float zMin;
	float zMax;
	uint regularGrid;

	uint attributes[];
	//int array containing attribute infos:
	//index attr 1
	//index attr 2
	//...
} info;

layout(std430, binding = 1) buffer IndexBuffer{
	uint ind[];
};

layout(std430, binding = 2) buffer DataBuffer{
	float data[];
};

layout(binding = 3, r16) uniform writeonly image3D densitys[];

layout (local_size_x = 256,local_size_y=1,local_size_z=1) in;

void main(){
	if(gl_GlobalInvocationID.x >= info.amtOfIndices) return;
	uint i = ind[gl_GlobalInvocationID.x];

	ivec3 pos;
	pos.x = int(((data[i*info.amtOfAttributes + info.xInd] - info.xMin) / (info.xMax - info.xMin)) * float(info.dimX));
	pos.y = int(((data[i*info.amtOfAttributes + info.yInd] - info.yMin) / (info.yMax - info.yMin)) * float(info.dimY));
	pos.z = int(((data[i*info.amtOfAttributes + info.zInd] - info.zMin) / (info.zMax - info.zMin)) * float(info.dimZ));
	if(pos.x<0||pos.y<0||pos.z<0||pos.x>=info.dimX||pos.y>=info.dimY||pos.z>=info.dimZ) return;

	//storing the data values
	for(int ind = 0;ind<info.amtOfBrushAttributes;++ind){
		float da = data[i*info.amtOfAttributes + info.attributes[ind]];
		imageStore(densitys[ind], pos, vec4(da));
		if(!bool(info.regularGrid) && pos.y >= info.dimY - 44){
			imageStore(densitys[ind], pos + ivec3(0,-1,0),vec4(da));
		}
	}
}